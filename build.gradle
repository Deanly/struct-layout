plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'org.jreleaser' version '1.16.0'
}

group = 'net.deanly'
version = '0.2.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.16'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.12'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testImplementation 'org.mockito:mockito-core:5.14.2'
}

tasks.withType(Test) {
    useJUnitPlatform()
}
tasks.named("build") {
    doFirst {
        file("$buildDir/jreleaser").mkdirs()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name.set('StructLayout')
                description.set('A Java library for structured binary data handling')
                url.set('https://github.com/deanly/struct-layout')
                inceptionYear.set('2025')
                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }
                scm {
                    connection.set('scm:git:https://github.com/deanly/struct-layout.git')
                    developerConnection.set('scm:git:ssh://github.com/deanly/struct-layout.git')
                    url.set('https://github.com/deanly/struct-layout')
                }
                developers {
                    developer {
                        id.set('deanly')
                        name.set('Dean Lee')
                        email.set('deanly.dev@gmail.com')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    gitRootSearch = true
    project {
        license.set("MIT")
    }
}
